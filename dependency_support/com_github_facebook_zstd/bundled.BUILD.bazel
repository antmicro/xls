# Copyright 2023 The XLS Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:public"])

licenses(["notice"])

# Builds everything together similarly to the zstd cmake file:
# https://github.com/facebook/zstd/blob/dev/build/cmake/lib/CMakeLists.txt
# but with legacy support and defines as in the zstd BUCK file:
# https://github.com/facebook/zstd/blob/dev/lib/BUCK

cc_library(
    name = "libzstd",
    srcs = glob([
        "lib/common/*.h",
        "lib/common/*.c",
        "lib/compress/*.h",
        "lib/compress/*.c",
        "lib/decompress/*.h",
        "lib/decompress/*.c",
        "lib/decompress/*.S",
        "lib/deprecated/*.h",
        "lib/deprecated/*.c",
        "lib/dictBuilder/*.h",
        "lib/dictBuilder/*.c",
        "lib/legacy/*.h",
        "lib/legacy/*.c",
    ]),
    hdrs = [
        "lib/zdict.h",
        "lib/zstd.h",
        "lib/zstd_errors.h",
    ],
    strip_include_prefix = "lib",
    linkopts = [
        "-pthread",
    ],
    local_defines = [
        "ZSTD_LEGACY_SUPPORT=4",
        "ZSTD_MULTITHREAD",
        "XXH_NAMESPACE=ZSTD_",
    ],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "programs",
    srcs = glob(
        [
            "programs/*.c",
            "programs/*.h",
        ],
        exclude = [
            "programs/zstdcli.c",
        ],
    ),
    deps = [
        ":libzstd",
    ],
)

cc_binary(
    name = "zstd",
    srcs = [
        "programs/zstdcli.c",
    ],
    deps = [
        ":programs",
    ],
    visibility = ["//visibility:public"],
)

# NOTE: Required because of direct zstd_compress.c include in decodecorpus sources
cc_library(
    name = "decodecorpus_includes",
    hdrs = [
        "lib/compress/zstd_compress.c",
    ],
)

cc_binary(
    name = "decodecorpus",
    srcs = [
        "tests/decodecorpus.c",
    ],
    deps = [
        ":programs",
        ":decodecorpus_includes",
    ],
    includes = [
        "lib/",
        "lib/common/",
        "lib/compress/",
        "lib/dictBuilder/",
        "lib/deprecated/",
        "programs/",
    ],
    visibility = ["//visibility:public"],
)
